//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'groovy-gradle-plugin'
    id 'com.gradleup.shadow' version '8.3.6'
    id "com.gradle.plugin-publish" version "1.3.1"
}
base.archivesName = 'ModAccessor'
group = 'io.github.forgestove'
version = '1.0.2'
repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    maven { url = 'https://maven.neoforged.net/releases' }
}
configurations {
    shade
    implementation.extendsFrom(shade)
}
java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
dependencies {
    compileOnly gradleApi()
    shade 'org.apache.commons:commons-lang3:3.18.0'
    shade "com.google.code.gson:gson:2.10"
    shade "org.ow2.asm:asm-util:9.7"
    shade("net.neoforged:accesstransformers:10.0.6") {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOnly('org.slf4j:slf4j-api:2.0.9')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}
shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shade]
    relocate 'net.neoforged.jst', 'io.github.forgestove.modaccessor.relocate.jst'
    relocate 'net.neoforged.accesstransformers', 'io.github.forgestove.modaccessor.relocate.accesstransformers'
    relocate 'org.objectweb.asm', 'io.github.forgestove.modaccessor.relocate.asm'
    relocate 'org.apache.commons.lang3', 'io.github.forgestove.modaccessor.relocate.commons.lang3'
}

gradlePlugin {
    website = 'https://github.com/ForgeStove/ModAccessor'
    vcsUrl = 'https://github.com/ForgeStove/ModAccessor'
    plugins {
        mod_accessor {
            displayName = 'ModAccessor'
            description = "A simple gradle plugin to solve the problem of accessing private fields and methods during compile time."
            id = 'io.github.forgestove.modaccessor'
            tags.addAll('neoforge', 'forge', 'minecraft', 'java')
            implementationClass = 'io.github.forgestove.modaccessor.ModAccessorPlugin'
        }
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
}
